// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

import {MCDevKit} from "devkit/MCDevKit.sol";
// Validation
import {check} from "devkit/error/Validation.sol";
// Utils
import {ForgeHelper} from "devkit/utils/ForgeHelper.sol";
import {Params} from "devkit/debug/Params.sol";
// Core
import {Function} from "devkit/ucs/functions/Function.sol";


/***********************************************
    üóÇÔ∏è Bundle Configuration
        üå± Init Custom Bundle
        üîó Use Function
            ‚ú® Add Custom Function
            üß∫ Add Custom Function to Bundle
        ü™ü Use Facade
************************************************/
library MCBundleLib {
    using MCBundleLib for MCDevKit;
    string constant LIB_NAME = "MCBundleLib";


    /**---------------------------
        üå± Init Custom Bundle
    -----------------------------*/
    function init(MCDevKit storage mc, string memory name) internal returns(MCDevKit storage) {
        uint pid = mc.recordExecStart("init", Params.append(name));
        mc.functions.safeInit(name);
        return mc.recordExecFinish(pid);
    }

    function init(MCDevKit storage mc) internal returns(MCDevKit storage) {
        return mc.init(mc.functions.genUniqueBundleName());
    }

    //
    function ensureInit(MCDevKit storage mc) internal returns(MCDevKit storage) {
        uint pid = mc.recordExecStart("ensureInit");
        if (mc.functions.notExistsCurrentBundle()) {
            mc.init();
        }
        return mc.recordExecFinish(pid);
    }


    /**---------------------
        üîó Use Function
    -----------------------*/
    function use(MCDevKit storage mc, string memory name, bytes4 selector, address implementation) internal returns(MCDevKit storage) {
        uint pid = mc.recordExecStart("use", Params.append(name, selector, implementation));
        return mc   .ensureInit()
                    .addFunction(name, selector, implementation)
                    .addCurrentToBundle()
                    .recordExecFinish(pid);
    }
    function use(MCDevKit storage mc, bytes4 selector, address implementation) internal returns(MCDevKit storage) {
        return mc.use(ForgeHelper.getLabel(implementation), selector, implementation);
    }
    function use(MCDevKit storage mc, Function storage functionInfo) internal returns(MCDevKit storage) {
        return mc.use(functionInfo.name, functionInfo.selector, functionInfo.implementation);
    }
    // function use(MCDevKit storage mc, Bundle storage bundleInfo) internal returns(MCDevKit storage) {
    //     return mc;
    // } TODO
    function use(MCDevKit storage mc, string memory name) internal returns(MCDevKit storage) {
        check(mc.functions.findFunction(name).isComplete(), "Invalid Function Name");
        return mc.use(mc.findFunction(name));
    }
        /**---------------------------
            ‚ú® Add Custom Function
        -----------------------------*/
        function addFunction(MCDevKit storage mc, string memory name, bytes4 selector, address implementation) internal returns(MCDevKit storage) {
            uint pid = mc.recordExecStart("addFunction");
            mc.functions.safeAddFunction(name, selector, implementation);
            return mc.recordExecFinish(pid);
        }
        /**-------------------------------------
            üß∫ Add Custom Function to Bundle
        ---------------------------------------*/
        function addToBundle(MCDevKit storage mc, Function storage functionInfo) internal returns(MCDevKit storage) {
            uint pid = mc.recordExecStart("addToBundle");
            mc.functions.addToBundle(functionInfo);
            return mc.recordExecFinish(pid);
        }
        function addCurrentToBundle(MCDevKit storage mc) internal returns(MCDevKit storage) {
            mc.functions.addToBundle(mc.findCurrentFunction());
            return mc;
        }


    /**------------------
        ü™ü Use Facade
    --------------------*/
    function useFacade(MCDevKit storage mc, string memory name, address facade) internal returns(MCDevKit storage) {
        uint pid = mc.recordExecStart("set");
        mc.functions.set(name, facade);
        return mc.recordExecFinish(pid);
    }

    function useFacade(MCDevKit storage mc, address facade) internal returns(MCDevKit storage) {
        return mc.useFacade(mc.functions.findCurrentBundleName(), facade);
    }

}
