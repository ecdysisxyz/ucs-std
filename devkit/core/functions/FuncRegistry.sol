// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

// Error & Debug
import {check} from "devkit/error/Validation.sol";
import {ERR, throwError} from "devkit/error/Error.sol";
import {Debug} from "devkit/debug/Debug.sol";
// Config
import {Config} from "../../Config.sol";
// Utils
import {StringUtils} from "../../utils/StringUtils.sol";
    using StringUtils for string;
import {BoolUtils} from "../../utils/BoolUtils.sol";
    using BoolUtils for bool;
// Core
import {FuncInfo} from "./FuncInfo.sol";
import {BundleInfo} from "./BundleInfo.sol";
import {MCStdFuncs} from "./MCStdFuncs.sol";


/****************************************
    üß© Meta Contract Functions Registry
*****************************************/
using FuncRegistryUtils for FuncRegistry global;
struct FuncRegistry {
    MCStdFuncs std;
    mapping(bytes32 nameHash => FuncInfo) customs;
    mapping(bytes32 nameHash => BundleInfo) bundles;
    string currentFunctionName;
    string currentBundleName;
}

library FuncRegistryUtils {
    string constant LIB_NAME = "FunctionRegistry";

    /**~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    << Primary >>
        üå± Init Bundle
        ‚ú® Add Custom Function
        üîè Load and Assign Custom Function from Env
        üß∫ Add Custom Function to Bundle
        üñº Set Facade
        üîº Update Current Context Function & Bundle
        üîç Find Function & Bundle
        üè∑ Generate Unique Name
    << Helper >>
        üîç Find Custom Function
    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~*/


    /**---------------------
        üå± Init Bundle
    -----------------------*/
    function init(FuncRegistry storage functions, string memory name) internal returns(FuncRegistry storage) {
        uint pid = functions.recordExecStart("init");
        functions.bundles[name.safeCalcHash()].safeAssign(name);
        functions.safeUpdateCurrentBundle(name);
        return functions.recordExecFinish(pid);
    }
    function safeInit(FuncRegistry storage functions, string memory name) internal returns(FuncRegistry storage) {
        uint pid = functions.recordExecStart("safeInit");
        check(name.isNotEmpty(), "Empty Name");
        return functions.assertBundleNotExists(name)
                        .init(name)
                        .recordExecFinish(pid);
    }


    /**---------------------------
        ‚ú® Add Custom Function
    -----------------------------*/
    function safeAddFunction(FuncRegistry storage functions, string memory name, bytes4 selector, address implementation) internal returns(FuncRegistry storage) {
        uint pid = functions.recordExecStart("safeAddFunction");
        check(name.isNotEmpty(), "Empty Name");
        functions.customs[name.safeCalcHash()]
                .safeAssign(name)
                .safeAssign(selector)
                .safeAssign(implementation);
        functions.safeUpdateCurrentFunction(name);
        return functions.recordExecFinish(pid);
    }


    /**---------------------------------------------
        üîè Load and Add Custom Function from Env
    -----------------------------------------------*/
    function safeLoadAndAdd(FuncRegistry storage functions, string memory envKey, string memory name, bytes4 selector, address implementation) internal returns(FuncRegistry storage) {
        uint pid = functions.recordExecStart("safeLoadAndAdd");
        functions.customs[name.safeCalcHash()]
                    .loadAndAssignFromEnv(envKey, name, selector);
        functions.safeUpdateCurrentFunction(name);
        return functions.recordExecFinish(pid);
    }


    /**-------------------------------------
        üß∫ Add Custom Function to Bundle
    ---------------------------------------*/
    function addToBundle(FuncRegistry storage functions, FuncInfo storage functionInfo) internal returns(FuncRegistry storage) {
        uint pid = functions.recordExecStart("addToBundle", "function");
        functions.findCurrentBundle().safeAdd(functionInfo);
        return functions.recordExecFinish(pid);
    }
    function addToBundle(FuncRegistry storage functions, FuncInfo[] storage functionInfos) internal returns(FuncRegistry storage) {
        uint pid = functions.recordExecStart("addToBundle", "functions"); // TODO params
        functions.findCurrentBundle().safeAdd(functionInfos);
        return functions.recordExecFinish(pid);
    }


    /**------------------
        üñº Set Facade
    --------------------*/
    function set(FuncRegistry storage functions, string memory name, address facade) internal returns(FuncRegistry storage) {
        uint pid = functions.recordExecStart("set");
        functions.bundles[name.safeCalcHash()]
                    .assertExists()
                    .safeAssign(facade);
        return functions.recordExecFinish(pid);
    }
    function set(FuncRegistry storage functions, address facade) internal returns(FuncRegistry storage) {
        uint pid = functions.recordExecStart("set");
        return functions.set(functions.findCurrentBundleName(), facade).recordExecFinish(pid);
    }


    /**------------------------------------------------
        üîº Update Current Context Function & Bundle
    --------------------------------------------------*/
    /**----- üß© Function -------*/
    function safeUpdateCurrentFunction(FuncRegistry storage functions, string memory name) internal returns(FuncRegistry storage) {
        uint pid = functions.recordExecStart("safeUpdateCurrentFunction");
        functions.currentFunctionName = name.assertNotEmpty();
        return functions.recordExecFinish(pid);
    }
    /**----- üß∫ Bundle -------*/
    function safeUpdateCurrentBundle(FuncRegistry storage functions, string memory name) internal returns(FuncRegistry storage) {
        uint pid = functions.recordExecStart("safeUpdateCurrentBundle");
        functions.currentBundleName = name.assertNotEmpty();
        return functions.recordExecFinish(pid);
    }


    /**-----------------------------------------------
        ‚ôªÔ∏è Reset Current Context Function & Bundle
    -------------------------------------------------*/
    function reset(FuncRegistry storage functions) internal returns(FuncRegistry storage) {
        uint pid = functions.recordExecStart("reset");
        delete functions.currentFunctionName;
        delete functions.currentBundleName;
        return functions.recordExecFinish(pid);
    }


    /**-------------------------------
        üîç Find Function & Bundle
    ---------------------------------*/
    /**----- üß© Function -------*/
    function findFunction(FuncRegistry storage functions, string memory name) internal returns(FuncInfo storage) {
        uint pid = functions.recordExecStart("findFunction");
        return functions.customs[name.safeCalcHash()].assertExists().recordExecFinish(pid);
    }
    function findCurrentFunction(FuncRegistry storage functions) internal returns(FuncInfo storage) {
        uint pid = functions.recordExecStart("findCurrentFunction");
        return functions.findFunction(functions.findCurrentFunctionName()).recordExecFinish(pid);
    }
        function findCurrentFunctionName(FuncRegistry storage functions) internal returns(string memory) {
            uint pid = functions.recordExecStart("findCurrentFunctionName");
            return functions.currentFunctionName.assertNotEmpty().recordExecFinish(pid);
        }

    /**----- üß∫ Bundle -------*/
    function findBundle(FuncRegistry storage functions, string memory name) internal returns(BundleInfo storage) {
        uint pid = functions.recordExecStart("findBundle");
        return functions.bundles[name.safeCalcHash()].recordExecFinish(pid);
    }
    function findCurrentBundle(FuncRegistry storage functions) internal returns(BundleInfo storage) {
        uint pid = functions.recordExecStart("findCurrentBundle");
        return functions.findBundle(functions.findCurrentBundleName()).recordExecFinish(pid);
    }
        function findCurrentBundleName(FuncRegistry storage functions) internal returns(string memory) {
            uint pid = functions.recordExecStart("findCurrentBundleName");
            return functions.currentBundleName.assertNotEmpty().recordExecFinish(pid);
        }


    /**-----------------------------
        üè∑ Generate Unique Name
    -------------------------------*/
    function genUniqueBundleName(FuncRegistry storage functions) internal returns(string memory name) {
        uint pid = functions.recordExecStart("genUniqueBundleName");
        Config.ScanRange memory range = Config.SCAN_RANGE();
        for (uint i = range.start; i <= range.end; ++i) {
            name = Config.DEFAULT_BUNDLE_NAME.toSequential(i);
            if (functions.existsBundle(name).isFalse()) return name.recordExecFinish(pid);
        }
        throwError(ERR.FIND_NAME_OVER_RANGE);
    }



    /**-------------------------------
        üßê Inspectors & Assertions
    ---------------------------------*/
    function existsBundle(FuncRegistry storage functions, string memory name) internal returns(bool) {
        return functions.bundles[name.safeCalcHash()].hasName();
    }
    function notExistsBundle(FuncRegistry storage functions, string memory name) internal returns(bool) {
        return functions.existsBundle(name).isNot();
    }
    function assertBundleNotExists(FuncRegistry storage functions, string memory name) internal returns(FuncRegistry storage) {
        check(functions.notExistsBundle(name), "Bundle Already Exists");
        return functions;
    }


    /**----------------
        üêû Debug
    ------------------*/
    /**
        Record Start
     */
    function recordExecStart(FuncRegistry storage, string memory funcName, string memory params) internal returns(uint) {
        return Debug.recordExecStart(LIB_NAME, funcName, params);
    }
    function recordExecStart(FuncRegistry storage functions, string memory funcName) internal returns(uint) {
        return functions.recordExecStart(funcName, "");
    }

    /**
        Record Finish
     */
    function recordExecFinish(FuncRegistry storage functions, uint pid) internal returns(FuncRegistry storage) {
        Debug.recordExecFinish(pid);
        return functions;
    }

}
