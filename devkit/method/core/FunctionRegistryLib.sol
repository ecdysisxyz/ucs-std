// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;

// Error & Debug
import {check} from "devkit/error/Validation.sol";
import {ERR, throwError} from "devkit/error/Error.sol";
import {Debug} from "devkit/debug/Debug.sol";
// Config
import {ScanRange, Config} from "devkit/config/Config.sol";
// Utils
import {StringUtils} from "devkit/utils/StringUtils.sol";
    using StringUtils for string;
import {BoolUtils} from "devkit/utils/BoolUtils.sol";
    using BoolUtils for bool;
// Core
import {Function} from "devkit/core/Function.sol";
import {Bundle} from "devkit/core/Bundle.sol";
import {StdFunctions} from "devkit/core/StdFunctions.sol";

import {FunctionRegistry} from "devkit/core/FunctionRegistry.sol";


/**~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    üìó Functions Registry
        ‚ú® Add Custom Function
        üîè Load and Assign Custom Function from Env
        üîº Update Current Context Function
        ‚ôªÔ∏è Reset Current Context Function & Bundle
        üîç Find Function
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~*/
library FunctionRegistryLib {
    /**---------------------------
        ‚ú® Add Custom Function
    -----------------------------*/
    function safeAddFunction(FunctionRegistry storage functions, string memory name, bytes4 selector, address implementation) internal returns(FunctionRegistry storage) {
        uint pid = functions.startProcess("safeAddFunction");
        check(name.isNotEmpty(), "Empty Name");
        functions.customs[name.safeCalcHash()]
                .safeAssign(name)
                .safeAssign(selector)
                .safeAssign(implementation);
        functions.safeUpdateCurrentFunction(name);
        return functions.finishProcess(pid);
    }


    /**---------------------------------------------
        üîè Load and Add Custom Function from Env
    -----------------------------------------------*/
    function safeLoadAndAdd(FunctionRegistry storage functions, string memory envKey, string memory name, bytes4 selector, address implementation) internal returns(FunctionRegistry storage) {
        uint pid = functions.startProcess("safeLoadAndAdd");
        functions.customs[name.safeCalcHash()]
                    .loadAndAssignFromEnv(envKey, name, selector);
        functions.safeUpdateCurrentFunction(name);
        return functions.finishProcess(pid);
    }


    /**------------------------------------------------
        üîº Update Current Context Function
    --------------------------------------------------*/
    function safeUpdateCurrentFunction(FunctionRegistry storage functions, string memory name) internal returns(FunctionRegistry storage) {
        uint pid = functions.startProcess("safeUpdateCurrentFunction");
        functions.currentName = name.assertNotEmpty();
        return functions.finishProcess(pid);
    }


    /**-----------------------------------------------
        ‚ôªÔ∏è Reset Current Context Function & Bundle
    -------------------------------------------------*/
    function reset(FunctionRegistry storage functions) internal returns(FunctionRegistry storage) {
        uint pid = functions.startProcess("reset");
        delete functions.currentName;
        return functions.finishProcess(pid);
    }


    /**-------------------------------
        üîç Find Function
    ---------------------------------*/
    function find(FunctionRegistry storage functions, string memory name) internal returns(Function storage) {
        uint pid = functions.startProcess("findFunction");
        return functions.customs[name.safeCalcHash()].assertExists().finishProcess(pid);
    }
    function findCurrentFunction(FunctionRegistry storage functions) internal returns(Function storage) {
        uint pid = functions.startProcess("findCurrentFunction");
        return functions.find(functions.findCurrentName()).finishProcess(pid);
    }
        function findCurrentName(FunctionRegistry storage functions) internal returns(string memory) {
            uint pid = functions.startProcess("findCurrentName");
            return functions.currentName.assertNotEmpty().recordExecFinish(pid);
        }

}
